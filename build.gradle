buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.4'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    repositories {
      mavenCentral()
    }

    dependencies {
        implementation 'com.google.code.findbugs:jsr305:3.0.2'
        implementation 'com.facebook.soloader:soloader:0.10.5'
    }

    compileSdkVersion 34
    buildToolsVersion "30.0.3"

    defaultConfig {
      minSdkVersion 19
      targetSdkVersion 34
    }

    sourceSets {

      main {
        manifest.srcFile 'AndroidManifest.xml'
        java.srcDirs = [
            'first-party/fbjni/java',
            'first-party/proguard/annotations',
            'java']
        jniLibs.srcDir 'libs'
        jni.srcDirs = []
      }
    }

    task ndkBuildRelease(type: Exec) {
      commandLine 'ndk-build',
        '-C', file('.').absolutePath,
       'NDK_APPLICATION_MK=${PWD}/native/Application.mk',
       'NDK_MODULE_PATH=${PWD}/native:${PWD}/first-party/fbjni/native:${PWD}/third-party',
       'APP_BUILD_SCRIPT=${PWD}/native/jni/Android.mk'
    }

    tasks.withType(JavaCompile) {
      compileTask -> compileTask.dependsOn ndkBuildRelease
    }

    lintOptions {
      abortOnError false
    }
}

// Configure gradle.properties to do maven builds
if (!project.hasProperty('sonatypeRepo') ||
    !project.hasProperty('sonatypeUsername') ||
    !project.hasProperty('sonatypePassword')) {
  return
}

// Maven Push

apply plugin: 'maven'
apply plugin: 'signing'

version = "2.0.4"
group = "com.facebook.conceal"

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            name 'Conceal'
            packaging 'aar'
            description 'Conceal is an easy to use, performant crypto library for android.'
            url 'http://facebook.github.io/conceal'

            scm {
                url 'scm:git@github.com:facebook/conceal.git'
                connection 'scm:git@github.com:facebook/conceal.git'
                developerConnection 'scm:git@github.com:facebook/conceal.git'
            }

            licenses {
                license {
                    name 'BSD 2-Clause License'
                    url 'https://github.com/facebook/conceal/blob/master/LICENSE'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'subodh'
                    name 'Subodh Iyengar'
                    email 'mani.subodh@gmail.com'
                }
            }
        }
    }
}
